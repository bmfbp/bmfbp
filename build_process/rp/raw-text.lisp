(in-package :arrowgrams/rp)

(defclass raw-text (e/part:code)
  ((buffer :accessor buffer)
   (state :accessor state)
   (nposition :accessor nposition)
   (nline :accessor nline)
   (start-position :accessor start-position)
   (start-line :accessor start-line)))
   
; (:code raw-text (:token) (:pull :out :error))

(defmethod get-ordered-buffer ((self raw-text)) (coerce (reverse (buffer self)) 'string))

(defmethod e/part:first-time ((self raw-text))
  (setf (buffer self) nil)
  (setf (state self) :idle)
  (setf (nposition self) 0)
  (setf (nline self) 0))

(defmethod e/part:react ((self raw-text) (e e/event:event))
  #+nil(format *standard-output* "~&raw-text gets ~S ~S~%" (@pin self e) (@data self e))
  (labels ((push-char-into-buffer () (push (token-text (@data self e)) (buffer self)))
           (pull () (@send self :pull :raw-text))
           (forward-token (&key (pulled-p nil))
             (let ((tok (@data self e)))
               (let ((new-token (make-token :kind (token-kind tok)
                                            :text (token-text tok)
                                            :line (token-line tok)
                                            :position (token-position tok)
                                            :pulled-p (or pulled-p (token-pulled-p tok)))))
                 (@send self :out new-token))))
           (start-char-p () 
             (when (eq :character (token-kind (@data self e)))
               (let ((c (token-text (@data self e))))
                 (char= c #\$))))
           (follow-char-p ()
             (when (eq :character (token-kind (@data self e)))
               (let ((c (token-text (@data self e))))
                 (not (char= c #\Newline)))))
           (action () (@pin self e))
           (next-state (x) (setf (state self) x))
           (eof-p () (eq :eof (token-kind (@data self e))))
           (record ()
             (setf (nline self) (token-line (@data self e))
                   (nposition self) (token-position (@data self e))))
           (clear-buffer ()
	     (setf (buffer self) nil))
           (send-raw-text ()
             (@send self :out (make-token :kind :raw :text (get-ordered-buffer self) :position (start-position self) :line (start-line self) :pulled-p t))
             (clear-buffer)))

    (ecase (state self)
      (:idle
       (ecase (action)
         (:token
          (record)
          (cond ((eof-p)
                 (forward-token)
                 (next-state :done))
		((start-char-p)
                 (setf (start-position self) (nposition self))
                 (setf (start-line self) (nline self))
                 (pull)
                 (next-state :collecting-text))
		(t (forward-token))))))
      (:collecting-text
       (ecase (action)
         (:token
          (record)
          (cond ((eof-p)
                 (send-raw-text)
                 (forward-token)
                 (next-state :done))
		((follow-char-p)
                 (push-char-into-buffer)
                 (pull))
		(t
                 (send-raw-text)
		 (forward-token)
		 (next-state :idle))))))
      (:done
       (@send self :error (format nil "raw-text finished, but received ~S" e))))))

(defmethod e/part:busy-p ((self raw-text))
  (call-next-method))
