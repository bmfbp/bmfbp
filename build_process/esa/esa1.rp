= esa-dsl
  @type-decls
  @kinds
  @aux
  @scripts
  EOF

- keyword-symbol
  [ ?SYMBOL
    [ ?SYMBOL/type ^ok
    | ?SYMBOL/kind ^ok
    | ?SYMBOL/end ^ok
    | ?SYMBOL/proto ^ok
    | ?SYMBOL/delegate ^ok
    | ?SYMBOL/method ^ok
    | ?SYMBOL/script ^ok
    | ?SYMBOL/aux ^ok
    | ?SYMBOL/let ^ok
    | ?SYMBOL/set ^ok
    | ?SYMBOL/defproto ^ok
    | ?SYMBOL/map ^ok
    | ?SYMBOL/in ^ok
    | ?SYMBOL/loop ^ok
    | ?SYMBOL/if ^ok
    | ?SYMBOL/then ^ok
    | ?SYMBOL/else ^ok
    | * ^fail
    ]
  | * ^fail
  ]

- non-keyword-symbol
  [ ?SYMBOL
    [ &keyword-symbol ^fail
    | * ^ok
    ]
  | * ^fail
  ]

= type-decls
  {[ ?SYMBOL/type @type-decl
   | * >
  ]}

= type-decl
  SYMBOL/type @esa-symbol

= kinds
  {[ ?SYMBOL/kind @kind
   | * >
  ]}

= aux
  {[ ?SYMBOL/aux @auxiliary
   | * >
  ]}

= scripts
  {[ ?SYMBOL/script @script-definition
   | * >
  ]}

= kind
  SYMBOL/kind
  @esa-symbol
       set-current-class
  {[ ?SYMBOL/script @script-decl
   | ?SYMBOL/method @method-decl
   | ?SYMBOL/delegate @delegate-def
   | ?SYMBOL/field @field-decl
   | ?SYMBOL/proto @proto-decl
   | * >
  ]}
       emit-methods
  SYMBOL/end SYMBOL/kind

= auxiliary
  SYMBOL/aux
  @esa-symbol
     set-current-class
  {[ ?SYMBOL/method @method-decl
   | ?SYMBOL/script @delegate-def
   | ?SYMBOL/script @script-decl
   | * >
  ]}
    emit-methods
  SYMBOL/end SYMBOL/aux

= field-decl
  SYMBOL/field
  [ ?SYMBOL/map @map-decl
  | &non-keyword-symbol @esa-symbol
  | *
  ]

= proto-decl
  SYMBOL/proto
  @esa-symbol

= delegate-def
  SYMBOL/delegate @esa-symbol
  push-symbol
  @typed-formals
  @return-type
  
= method-decl
  SYMBOL/method @esa-symbol
  @typed-formals
  @return-type

  
= script-decl
  SYMBOL/script @esa-symbol
  @typed-formals
  @return-type

= map-decl
  SYMBOL/map 
  @esa-symbol
  
= typed-formals
  [ ?'(' '(' type-list ')'
  | *
  ]

= type-list
  @esa-symbol
  {[ &non-keyword-symbol @esa-symbol
   | * >
  ]}
  
= return-type
  [ ?'>' '>' '>'
         [ ?SYMBOL/map SYMBOL/map @esa-symbol
         | * @esa-symbol
]
  | *
  ]
  
= script-definition
  SYMBOL/script
  @qualified-symbol
  set-current-method
  @formals
  @return-type
  @script-body
  SYMBOL/end SYMBOL/script

= formals
  {[ ?'(' '(' untyped-formals ')'
   | * >
  ]}

= untyped-formals
  {[ &non-keyword-symbol @esa-symbol 
   | * >
  ]}

= script-body
  {
   [ ?SYMBOL/let @let-statement
   | ?SYMBOL/map @map-statement
   | ?SYMBOL/set @set-statement
   | ?SYMBOL/defproto @set-proto-statement
   | ?SYMBOL/if @if-statement
   | ?SYMBOL/loop @loop-statement
   | ?SYMBOL/exit-when @exit-when-statement
   | ?'>' @return-statement
   | ?'@' @esa-expr
   | &non-keyword-symbol @esa-expr
   | * >
  ]}

= let-statement
  SYMBOL/let
   @esa-symbol
   '=' 
   @esa-expr
   SYMBOL/in 
   @script-body
   SYMBOL/end SYMBOL/let

= set-statement
  SYMBOL/set
   @esa-expr
   '=' 
   @esa-expr

= set-proto-statement
  SYMBOL/defproto
   @esa-expr
   '=' 
   @esa-expr
  
= map-statement
  SYMBOL/map @esa-symbol
  '='
  @esa-expr
  SYMBOL/in @script-body
  SYMBOL/end SYMBOL/map

= loop-statement
  SYMBOL/loop
    @script-body
  SYMBOL/end SYMBOL/loop
  
= exit-when-statement
  SYMBOL/exit-when
    @esa-expr

= if-statement
  SYMBOL/if
    @esa-expr
  SYMBOL/then
    @script-body
  [ ?SYMBOL/else SYMBOL/else
     @script-body
  | *
  ]
  SYMBOL/end SYMBOL/if
  
= script-call
  '@' @qualified-symbol
  
= method-call
  @qualified-symbol

= return-statement
  '>' '>' @esa-symbol

= qualified-symbol
  @esa-symbol
    set-current-class
  [ ?'.' @dotted-symbol
  | *
  ]

= qualifiers
  {[ ?'.' @dotted-symbol
   | * >
  ]}
  
= dotted-symbol
  '.' @esa-symbol

= esa-symbol
  [ &non-keyword-symbol
    clear-saved-text
    SYMBOL
    @esa-symbol-follow
  | *
  ]

= esa-symbol-follow
  save-text
  {[ ?'/' '/' combine-text SYMBOL combine-text
   | ?'-' '-' combine-text SYMBOL combine-text
   | ?'?' '?' combine-text >
   | * >
  ]}

= esa-expr
  expr-stack-open
  [ ?'@' '@' set-call-rule-flag | * ]
  @esa-symbol
  push-symbol-onto-expr-stack
  {[ ?'.' @dotted-symbol push-symbol-onto-expr-stack
   | * >
  ]}
  emit-expr-stack
  @actuals
  expr-stack-close

= actuals
 [ ?'(' '(' {[ &non-keyword-symbol @esa-symbol emit-expr-actual | * > ]} ')'
 | *
 ]

